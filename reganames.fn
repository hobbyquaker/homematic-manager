!# reganames.fn 1.1 homematic-manager
!#
!# Dieses Homematic-Script gibt eine Liste aller Geraete-/Kanal-Namen als JSON String aus
!#
!# 5'2014 hobbyquaker https://github.com/hobbyquaker
!#

string sDevId;
string sChnId;

Write('{');

boolean dFirst = true;

foreach (sDevId, root.Devices().EnumUsedIDs()) {

    object oDevice   = dom.GetObject(sDevId);
    object oChannel;

    if (dFirst) {
        dFirst = false;
    } else {
        WriteLine(',');
    }

    Write('"' # oDevice.Address() # '":{"ID":"' # oDevice.ID() # '","Name":"');
    WriteURL(oDevice.Name());
    Write('"}');

    foreach(sChnId, oDevice.Channels()) {
        oChannel = dom.GetObject(sChnId);
        WriteLine(",");
        Write('"' # oChannel.Address() # '":{"ID":"' # oChannel.ID() # '","Name":"');
        WriteURL(oChannel.Name());
        Write('"}');
    }

}
Write('}');
